#!/bin/fish
# base installation for my awesome config
# surely there being package that installed before
# like: git vim yadm

sudo pacman -Syy --needed git yadm which base-devel python
set PARU_PATH "$HOME/.local/lib/paru"
which paru &>/dev/null || begin
    git clone https://aur.archlinux.org/paru.git "$PARU_PATH"
    cd "$PARU_PATH" || exit 1
    makepkg -si
end

which powershell.exe &>/dev/null && set is_wsl true || set is_wsl false
echo "is wsl: " $is_wsl

set 2install

# terminal
set -a 2install alacritty dracula-alacritty-git starship
# editors
set -a 2install neovim emacs
# better alternative command
set -a 2install fd ripgrep exa bat bpytop
# gtk theme and icon
set -a 2install whitesur-gtk-theme papirus-icon-theme-git bibata-cursor-theme-bin
# English serif and sans-serif fonts
set -a 2install ttf-linux-libertine gentium-plus-font adobe-source-serif-fonts
# coding fonts
set -a 2install nerd-fonts-{mononoki,fira-code} ttf-recursive
# symbol and unicode fonts
set -a 2install ttf-symbola

# wsl have built in x server so display and windows manager are not needed
if not $is_wsl
    # X and windows manager
    set -a 2install linux-headers nvidia-dkms nvidia-settings xorg-server awesome-git lain-git picom-ibhagwan-git
    # display manager and screen locker
    set -a 2install lightdm lightdm-webkit-theme-aether betterlockscreen xidlehook
    # gui system tools
    set -a 2install redshift pamac-aur rofi gparted xcolor feh zenity xcape
    set -a 2install polkit-kde-agent gnome-keyring
    # proxy
    set -a 2install xray-bin proxychains-ng qv2ray
    # symbol and unicode fonts for desktop environment
    set -a 2install ttf-material-design-icons-git ttf-twemoji
else
    # subsystemctl, for ssh into wsl
    set subspath "~/.local/lib/subsystemctl"
    ls "$subspath" &>/dev/null || begin
        curl -Lo "$subspath"/PKGBUILD --create-dirs https://github.com/sorah/arch.sorah.jp/blob/master/aur-sorah/PKGBUILDs/subsystemctl/PKGBUILD && cd "$subspath" && makepkg -si
    end
end

paru -S --needed $2install

set COMICSHANNS_PATH "$HOME/.local/lib/commic_shanns"
fc-list | grep "comic shanns" &>/dev/null || begin
    git clone https://github.com/shannpersand/comic-shanns.git "$COMICSHANNS_PATH"
    cd "$COMICSHANNS_PATH" || exit 1
    cp v2/* ~/.local/share/fonts
end

# doom emacs
which doom &>/dev/null || begin
    git clone --depth 1 https://github.com/hlissner/doom-emacs ~/.emacs.d
    set -a fish_user_paths ~/.emacs.d/bin
    doom install
end

# neovim plugin
ls ~/.local/share/nvim/site/autoload/plug.vim &>/dev/null || begin
    sh -c 'curl -fLo "${XDG_DATA_HOME:-$HOME/.local/share}"/nvim/site/autoload/plug.vim --create-dirs \
       https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
end
# then ":PlugInstall" when open neovim
